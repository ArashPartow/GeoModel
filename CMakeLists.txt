# Copyright (C) 2002-2019 CERN for the benefit of the ATLAS collaboration

# Set up the project.
cmake_minimum_required( VERSION 3.1 )
project( "GeoModelCore" VERSION 3.2.0 LANGUAGES CXX )

# ---------------------------

# Make the module directory visible to CMake.
list( APPEND CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake )

# Set build and C++ default options
include ( configure_cpp_options )

# Use the GNU install directory names.
include( GNUInstallDirs )

# Set up how the project would use Eigen.
include( SetupEigen3 )

# Print Build Info on screen
include( PrintBuildInfo )

# ---------------------------

# Set up the build of the two libraries of the project.
add_subdirectory( GeoGenericFunctions )
add_subdirectory( GeoModelKernel )

# Create and install the version description of the project.
include( WriteBasicConfigVersionFile )
write_basic_config_version_file(
   ${CMAKE_BINARY_DIR}${CMAKE_FILES_DIRECTORY}/${PROJECT_NAME}ConfigVersion.cmake
   VERSION ${PROJECT_VERSION}
   COMPATIBILITY SameMajorVersion )
install(
   FILES
   ${CMAKE_BINARY_DIR}${CMAKE_FILES_DIRECTORY}/${PROJECT_NAME}ConfigVersion.cmake
   COMPONENT Development
   DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/${PROJECT_NAME} )

# Create and install the description of the libraries.
install( EXPORT ${PROJECT_NAME}-export
   FILE ${PROJECT_NAME}Targets.cmake
   COMPONENT Development
   NAMESPACE "${PROJECT_NAME}::"
   DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/${PROJECT_NAME} )

# Install the hand-written project configuration.
configure_file( ${CMAKE_SOURCE_DIR}/cmake/GeoModelCoreConfig.cmake.in
   ${CMAKE_BINARY_DIR}${CMAKE_FILES_DIRECTORY}/${PROJECT_NAME}Config.cmake
   @ONLY )
install(
   FILES
   ${CMAKE_BINARY_DIR}${CMAKE_FILES_DIRECTORY}/${PROJECT_NAME}Config.cmake
   COMPONENT Development
   DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/${PROJECT_NAME} )



# Export targets from the *build* tree for use by outside projects.
#
# INFO:
# Thus, this project can be used by other projects without being installed.
# This is useful for development: you can build a child projet without an
# install path.
# For example:
# while generating GeoModel projects with CMake as Xcode project on macOS,
# after having built GeoModelCore, you can use that to build GeoModelIO without
# the need of an installation step, by simply prepend 'GeoModelCore_DIR' as this:
#     $ GeoModelCore_DIR=../b_core/CMakeFiles cmake -G Xcode ../../GeoModelIO
# which is useful when generating projects
# that needs to be built with Xcode. Xcode in fact does not handle well the install
# folder generated by CMake: sometimes the content of that is not updated when
# updating the code, which results in keeping running the same old code even if
# we modified our source code.
# export(EXPORT ${PROJECT_NAME}-export NAMESPACE "${PROJECT_NAME}::" FILE ${CMAKE_BINARY_DIR}${CMAKE_FILES_DIRECTORY}/${PROJECT_NAME}Config.cmake)
export(TARGETS GeoGenericFunctions GeoModelKernel NAMESPACE "${PROJECT_NAME}::" FILE ${CMAKE_BINARY_DIR}${CMAKE_FILES_DIRECTORY}/${PROJECT_NAME}Targets.cmake)


# Set up the packaging of the project using CPack.
include( GeoModelCorePackaging )


#################################
##### DOXYGEN DOCUMENTATION #####
#################################

# NOTE:
# We want to build the documentation only if we run "make doc"

# first we can indicate the documentation build as an option and set it to ON by default
option(BUILD_DOC "Build documentation" ON)

# check if Doxygen is installed
find_package(Doxygen)
if (DOXYGEN_FOUND)
  # set input and output files
  set(DOXYGEN_IN ${CMAKE_CURRENT_SOURCE_DIR}/docs/Doxyfile.in)
  set(DOXYGEN_OUT ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile)

  # request to configure the file
  configure_file(${DOXYGEN_IN} ${DOXYGEN_OUT} @ONLY)

  # note:
  # - `make doc` builds the Doxygen docs on request
  # - `make all` allows to build the docs together with the application
  add_custom_target( doc
      COMMAND ${DOXYGEN_EXECUTABLE} ${DOXYGEN_OUT}
      WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
      COMMENT "Generating API documentation with Doxygen"
      VERBATIM )
else (DOXYGEN_FOUND)
  message("Doxygen need to be installed to generate the doxygen documentation")
endif (DOXYGEN_FOUND)
